--************************************************************--
--************************************************************--
--
--   Sample example for full reachability
--   Universite Paris 13, Master 2 SITH
--
--   Etienne ANDRE
--   Laboratoire d'Informatique de Paris Nord
--
--                                Created : 2012/02/08
--                          Last modified : 2014/03/27
--************************************************************--
--************************************************************--

var
 	x1, x2
		: clock;

	p1, p2
		: parameter;


--************************************************************--
  automaton A
--************************************************************--
synclabs: ;

loc q0: while x1 <= 2 p1 wait {}
	when True do {x2' = 0} goto q1;
	when x1 >= p2 do {} goto qBad;

loc q1: while True wait {}
	when True do {x2' = 0} goto q1;
	when x1 >= p1 do {} goto q2;

loc q2: while x1 <= p2 wait {}
	when x2 > p2 do {} goto q3;
	when x1 >= 4p1 do {} goto qBad;

loc q3: while True wait {}
	when True do {} goto q2;
	
loc qBad: while True wait {}

end -- A


--************************************************************--
--************************************************************--
-- ANALYSIS
--************************************************************--
--************************************************************--

var init : region;

init := True
	----------------------
	-- Initial locations
	----------------------
	& loc[A]      = q0

	----------------------
	-- Clocks
	----------------------
	& x1 = x2
	& x1 >= 0
	
	----------------------
	-- Given constraints
	----------------------
	& p1 >= 0
	& p2 >= 0

	----------------------
	-- Instantiations
	----------------------

;


property := unreachable loc[A] = qBad;
